// Each #kernel tells which function to compile; you can have many kernels
#pragma multi_compile_local __ USE_BVH
#pragma multi_compile_local __ BACKFACECULLING
#pragma multi_compile_local __ USE_LIGHTS

#pragma kernel CSMain

#include "Defines.compute"
#include "Materials.compute"

RWTexture2D<float4> Result;

RWTexture2D<float4> PrevResult;
float _CurrentSample;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
float2 _PixelOffset;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

/*
* 0 : Shaded
* 1 : Normals
* 2 : UVs
* 3 : WorldPosition
*/
int OutputType;

Ray CreateCameraRay(float2 uv)
{
	//extract translation component from camera to World
	float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;

	//get view-space direction from ndc
	float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
	//transform view-space direction to world-space direction
	direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
	//normalize the direction
	direction = normalize(direction);

	Ray ray = CreateRay(origin, direction, RAYTYPE_PRIMARY);
	return ray;
}

void Trace(Ray ray, inout RayHit bestHit, inout float t)
{
    t = 1.#INF;
	bestHit = CreateRayHit();
		
#ifdef USE_BVH
	BVH b;
	iGeometry geometry = b;
#else
	Meshes m;
	iGeometry geometry = m;
#endif
	geometry.Intersect(ray, bestHit, t);


#ifdef USE_LIGHTS
	IntersectLights(ray, bestHit, t);
#endif

}

float3 Shade(inout Ray ray, RayHit hit, float t, inout RayBuffer rb)
{	if(any(hit.lightContrib))
	{
		return hit.lightContrib;
	}
	if (hit.matID < MAX_MATERIALS)
	{
		float3 attenuation = float3(0.0f, 0.0f, 0.0f);
        
		switch(OutputType)
		{

			case 1:
				attenuation = hit.normal * 0.5 + 0.5;
				break;
			case 2:
				attenuation = float3(hit.uv, 0.0);
				break;
			case 3:
				attenuation = hit.normal;
				break;			
			case 0:  

			default:      
				BufferMaterial mat;
				mat.Scatter(hit.matID, ray, hit, attenuation, rb);
				break;
		}


		return attenuation;
	}
	else {
		//render sky
		// Sample the skybox and write it
		float theta = acos(ray.direction.y) / -PI;
		float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
		return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
	}
}

[numthreads(8, 8, 1)]
void CSMain(uint3 groupid : SV_GroupThreadID, uint3 dpID : SV_DispatchThreadID)
{
	uint width, height;
	Result.GetDimensions(width, height);
	_Pixel = dpID.xy;


	// Transform pixel to [-1,1] range
    float2 uv = float2((dpID.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);

	// Get a ray for the UVs
	//Ray ray = CreateCameraRay(uv);
	RayHit hit = CreateRayHit();
	RayBuffer rb = CreateRayBuffer();
	rb.Push(CreateCameraRay(uv));

	float t = 1.#INF;	
        
    //Trace and Shade 
    float3 result = float3(0.0f, 0.0f, 0.0f);
	[unroll(10)]
	while (rb.currentSize > 0)
	{
		Ray ray = rb.Pop();		
		if (any(ray.energy < 0))
			continue;
		Trace(ray, hit, t);
        result += ray.energy * Shade(ray, hit, t, rb);
	}
	result = max(result, 0.0f);

    //write result
    float3 prevResult = PrevResult[dpID.xy].rgb;
    float3 finalResult = (prevResult * (_CurrentSample) + result) / (_CurrentSample + 1.0);
    Result[dpID.xy] = float4(finalResult, 1.0f);
    PrevResult[dpID.xy] = float4(finalResult, 1.0f);


}
